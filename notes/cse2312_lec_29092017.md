CSE 2312
Lecture Notes
29. September 2017
====

Strings
===

```arm7

	.data
string:
  .ascii "abc"          @ 3 characters (bytes)
  .byte  97, 98, 99     @ 3 bytes (characters)
```

The three characters (abc) and the 3 bytes (97, 98, 99) have the same bit
pattern in memory, and can be interpreted either as numbers, or characters
depending on what the program is doing. This is no different than assigning
a `char` in C to have the value of a number (e.g. `char a = 97;`), or an `int`
to have the value of a character (e.g. `int a = 'a';`).

Strings in C are almost always null-terminated (ending with a `\0` that is 
never printed). We can do the same thing in assembly in two ways.

```arm7
	.data
string2:
  .asciz "abc"          @ 4 characters (.asciz will add the \0 to the end)
  .byte  97, 98, 99, 0  @ 4 bytes (null terminator manually added)
```

While the `.ascii` directive will allocate only enough space for the number of
characters in the string, the `.asciz` directive will allocated enough space
for the number of characters in the string literal + 1 for the null terminator
is will automatically add.

Both `.ascii` and `.asciz` should be preferred to manually writing the numeric
values for the ascii characters out as shown in the `.byte` directives, because
it's actually readable to someone who doesn't have the entire ascii table
memorized. 

Using STDIN and STDOUT
===

`STDIN` is the C term for the standard input (the terminal input).  
`STDOUT` is the C term for the standard output (the terminal output).  

Writing to `STDOUT` and reading from `STDIN` are handled through system calls
(the same way we exit the program), via the `swi` opcode. We have to use a few
registers to communicate with `swi` what actions we want to perform, similar to
how we `mov r7, #1` before calling `swi 0` to exit the program.

`r7`
---
These codes can be looked up by googling "linux system calls."
* 3 - read
* 4 - write

`r0`
---
This is interpreted as the file pointer, luckily for us STDIN and STDOUT have
constant file pointers:
* 0 - STDIN
* 1 - STDOUT

`r2`
---
This is the number of characters (bytes) to read/write from/to the file
described by `r0`.

`r1`
---
This is the address of the string to print to STDOUT, or the location to
store the values read in from STDIN. You can use `ldr` to load the address
of the register into `r1` before this.


Example 1
===

```arm7
	.global _start

_start:
  mov r7, #4                    @    write...
  mov r2, #3                    @ ...3 characters...
  mov r0, #1                    @ ...to STDOUT...
  ldr r1, =stringLetters        @ ...starting at stringLetters
  swi 0                         @ (do it)

  mov r7, #4                    @    write...
  mov r2, #1                    @ ...1 character...
  mov r0, #1                    @ ...to STDOUT...
  ldr r1, =newLine              @ ...starting at newLine
  swi 0                         @ (do it)

  mov r7, #4                    @    write...
  mov r2, #3                    @ ...3 characters...
  mov r0, #1                    @ ...to STDOUT...
  ldr r1, =stringNumbers        @ ...starting at stringNumbers
  swi 0                         @ (do it)

_exit:
  mov r7, #1
  swi 0

	.data
stringLetters:
	.ascii "abc"

stringNumbers:
	.byte  65, 66, 67 @ "ABC"

newLine:
	.ascii "\n"
```

When run, this program produces:
```
>> abc
>> ABC
```
(`>>` denotes output, `<<` denotes input)

There was no trailing new line after ABC, so the prompt looked likes this:
```
pi@raspberry:~$ ./a.out
abc
ABC pi@raspberry:~$ 
```

Example 2
===

```arm7
	.global _start

_start:
  @ read some stuff from the user
  mov r7, #3            @    read...
  mov r2, #20           @ ...20 characters...
  mov r0, #0            @ ...from stdin...
  ldr r1, =string       @ ...into string
  swi 0                 @ (do it)

  @ print an empty line
  mov r7, #4            @ write
  mov r2, #1            @ ...1 character...
  mov r0, #1            @ ...to stdout...
  ldr r1, =newLine      @ ...starting at newLine
  swi 0                 @ (do it)

  @ echo what the user wrote
  mov r7, #4            @ write
  mov r2, #20           @ ...20 characters...
  mov r0, #1            @ ...to stdout...
  ldr r1, =string       @ ...starting at strings
  swi 0                 @ (do it)

_exit:
  mov r7, #1
  swi 0

	.data
string:
	.space 20  @ space for 20 characters
        @ this will be a series of \0's, which will tell the OS tostop
        @ printing if the user entered fewer than 20 characters

newLine:
	.ascii "\n"
```

If we give this fewer than 20 characters, it'll work like we expect it to:
```
<< this is a string
>> 
>> this is a string
```
(one again, with no trailing newline, so the prompt starts immediately)

If we give it more than 20 characters, it'll stop at 20 and interact oddly with
the prompt:
```
<< this is a string that's too long
>>
>> this is a string that
```
And with the prompt it looks like:
```
pi@raspberry:~$ ./a.out
this is a string that's too long

this is a string that pi@raspberry:~$ 's too long
```

We ended class after he was going on a tangent about people turning in
assignments that could've been completed in like 20 lines that took them
700+ because they didn't properly use conditionals. The point was to make you
realize that you DO NOT want to write a ton of code for the homework assignment.
Hardcode as few limitations on your program as you possibly can, so if he were
to change the input your entire program wouldn't break.  

